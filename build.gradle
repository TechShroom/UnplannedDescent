def subdeps = [:]

File[] dirList(String dir) {
    file(dir).listFiles({file -> file.isDirectory() && !file.getName().startsWith(".")} as FileFilter).sort()
}

dirList(".").each{
    subdeps[it.getName()] = []
}

subdeps.entrySet().each{
    if (it.key != "UDCore")
        it.value.add("UDCore")
}

subprojects {
    println "=== Configuring $name ==="
    apply plugin: 'java'
    apply plugin: 'eclipse'
    
    // eclipse bug workaround
    task reeclipse(dependsOn: ['cleanEclipseClasspath', 'eclipse'])

    repositories {
        mavenCentral()
    }

    dependencies {
        compile group: 'net.sf.sociaal', name: 'lwjgl', version: '3.+'

        subdeps[name].each{
            compile project(":$it")
        }

        testCompile group: 'junit', name: 'junit', version: '4.+'
    }
    println '=== Complete ==='
}

buildscript {
    repositories {
        mavenCentral()
    }
    
    
    dependencies {
        classpath 'org.ajoberstar:gradle-git:0.9.0'
    }
}

import org.ajoberstar.grgit.*

ext.repo = Grgit.open(project.file('.'))

version = "1.0.0-${repo.head().abbreviatedId}"

task tagRelease << {
  repo.tag.add {
    name = version
    message = "Release of ${version}"
  }
}
