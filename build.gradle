apply from: 'util.gradle', to: project

if (!project.hasJDK6RT()) {
    throw new RuntimeException("Need to know where your JDK6 rt.jar is! " +
    "(set JAVA6_HOME to a jdk6 home or set JDK6_RT to the rt.jar)")
}

def oauth = ""
version = "1.0.0"

task doBuild << {
    subprojects {
        build.execute()
    }
}

apply from: 'github.gradle', to: project

def subdeps = [:]
def submvn = [:]

File[] dirList(String dir) {
    file(dir).listFiles({file -> file.isDirectory() && !file.getName().startsWith(".")} as FileFilter).sort()
}

dirList(".").each{
    subdeps[it.getName()] = []
    submvn[it.getName()] = []
}

subdeps.entrySet().each{
    if (it.key != "UDCore")
        it.value.add("UDCore")
}

submvn["UDCore"].add("net.sf.sociaal:lwjgl:3.+")

subprojects {
    println "=== Configuring $name ==="
    apply plugin: 'java'

    // java 6 compat
    compileJava.sourceCompatibility = '1.6'
    compileJava.options.bootClasspath = getJDK6RT()

    apply plugin: 'eclipse'
    
    // eclipse bug workaround
    task reeclipse(dependsOn: ['cleanEclipseClasspath', 'eclipse'])

    repositories {
        mavenCentral()
    }

    dependencies {
        submvn[name].each{
            compile "$it"
        }

        subdeps[name].each{
            compile project(":$it")
        }

        testCompile group: 'junit', name: 'junit', version: '4.+'
    }
    println '=== Complete ==='
}
