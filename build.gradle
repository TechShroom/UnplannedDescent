buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    configurations.all {
        resolutionStrategy {
            cacheChangingModulesFor 0, 'seconds'
        }
    }
    dependencies {
        classpath group: 'com.techshroom', name: 'Aversion', version: '3.0.1'
        classpath "gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin:0.14.0"
        classpath "com.github.jengelman.gradle.plugins:shadow:2.0.1"
    }
}
ext.lwjglVersion = '3.1.3'

def addLWJGL(project, ext, natives) {
    // "lwjgl" or "lwjgl-ext"
    def lwjgl = "lwjgl" + (ext ? "-" + ext : "")
    def ver = project.lwjglVersion
    project.dependencies {
        compile group: 'org.lwjgl', name: lwjgl, version: ver
        if (natives) {
            compile group: 'org.lwjgl', name: lwjgl, version: ver, classifier: 'natives-macos'
            compile group: 'org.lwjgl', name: lwjgl, version: ver, classifier: 'natives-linux'
            compile group: 'org.lwjgl', name: lwjgl, version: ver, classifier: 'natives-windows'
        }
    }
}
subprojects {
    version = rootProject.version

    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'aversion-util'
    apply plugin: 'aversion-maven'
    apply plugin: 'aversion-apt'
    apply plugin: "com.github.hierynomus.license"

    license {
        ext {
            name = rootProject.name
            organization = rootProject.organization
            url = rootProject.url
        }
        header = rootProject.file('HEADER.txt')
        ignoreFailures = false
        strictCheck = true
        include '**/*.java'
        exclude '**/Sync.java'
        exclude '**/Color.java'
        mapping {
            java = 'SLASHSTAR_STYLE'
        }
    }

    util {
        javaVersion = '1.8'
    }

    mavencfg {
        coords 'TechShroom', 'UnplannedDescent'
        description 'UnplannedDescent'
        artifactName = rootProject.name + '.' + project.name
    }

    repositories {
        mavenCentral()
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    configurations.all {
       resolutionStrategy {
         // fail eagerly on version conflict (includes transitive dependencies)
         // e.g. multiple different versions of the same dependency (group and name are equal)
         failOnVersionConflict()

         // force certain versions of dependencies (including transitive)
         force 'com.google.guava:guava:23.0', 'com.google.code.findbugs:jsr305:3.0.1', 'com.google.auto:auto-common:0.8',
                'com.google.auto.value:auto-value:1.5.2', 'com.google.errorprone:error_prone_annotations:2.0.19'

         cacheDynamicVersionsFor 10, 'minutes'
         // don't cache changing modules at all
         cacheChangingModulesFor 0, 'seconds'
       }
    }
    dependencies {
        compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
        compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
        compile group: 'ch.qos.logback', name: 'logback-core', version: '1.2.3'

        compile group: 'com.google.code.gson', name: 'gson', version: '2.8.2'

        compile group: 'com.techshroom', name: 'jsr305-plus', version: '0.0.1'

        def guice = [group: 'com.google.inject', name: 'guice', version: '4.1.0']
        if (project.findProperty("debugging")) {
            println "Using no_aop guice for debugging!"
            guice.put('classifier', 'no_aop')
        }
        compile guice

        compile group: 'com.squareup', name: 'javapoet', version: '1.9.0'

        compile group: 'com.squareup.okio', name: 'okio', version: '1.13.0'

        compile group: 'com.flowpowered', name: 'flow-math', version: '1.0.3'

        addAPTReqWComp group: 'com.google.guava', name: 'guava', version: '23.0'
        addAPT group: 'com.google.auto.service', name: 'auto-service', version: '1.0-rc4'
        addAPT group: 'com.google.auto.value', name: 'auto-value', version: '1.5.4'
        addAPTReq name: 'aopalliance'
        addAPTReq name: 'asm'
        addAPTReq name: 'auto-value'
        addAPTReq name: 'auto-common'
        addAPTReq name: 'cglib'
        addAPTReq name: 'commons-collections'
        addAPTReq name: 'commons-lang'
        addAPTReq name: 'dagger'
        addAPTReq name: 'javawriter'
        addAPTReq name: 'javax.inject'
        addAPTReq name: 'velocity'

        testCompile group: 'junit', name: 'junit', version: '4.12'
    }

	javadoc {
        options.addStringOption('Xdoclint:none', '-quiet')
    }

    configure([compileJava, compileTestJava]) {
        options.compilerArgs += ['-Xlint:all', '-Xlint:-processing', '-Xlint:-path']
        options.deprecation = true
        options.encoding = 'UTF-8'
        options.incremental = true
        options.fork = true
    }
}

project(':api') {
    dependencies {
        compile group: 'net.java.dev.jna', name: 'jna', version: '4.5.0'
        compile group: 'com.github.luben', name: 'zstd-jni', version: '1.3.2-2'
        compile 'org.eclipse.collections:eclipse-collections-api:9.0.0'
        compile 'org.eclipse.collections:eclipse-collections:9.0.0'
        addLWJGL(project, '', true);
        addLWJGL(project, 'stb', true);
    }
}

project(':ap') {
    dependencies {
        compile project(':api')
        compile group: 'com.google.auto', name: 'auto-common', version: '0.8'

        testCompile group: 'com.google.testing.compile', name: 'compile-testing', version: '0.12'
    }
}

project(':gui') {
    dependencies {
        compile project(':api')
        apt project(':ap')
        compileOnly project(':ap')
    }
}

project(':implementation') {
    dependencies {
        addLWJGL(project, '', true);
        addLWJGL(project, 'opengl', true);
        addLWJGL(project, 'openal', true);
        addLWJGL(project, 'glfw', true);
        addLWJGL(project, 'nanovg', true);
        addLWJGL(project, 'stb', true);

        compile project(':api')
    }
}
project(':implementation-gui') {
    dependencies {
        compile project(':implementation')
        compile project(':gui')
    }
}
project(':examples') {
    dependencies {
        compile project(':api')
        compile project(':gui')
        apt project(':ap')
        compileOnly project(':ap')
        runtime project(':implementation')
    }
    apply plugin: 'com.github.johnrengelman.shadow'
    jar {
        manifest {
            attributes 'Main-Class': 'com.techshroom.unplanned.examples.ExamplePicker'
        }
    }
}
